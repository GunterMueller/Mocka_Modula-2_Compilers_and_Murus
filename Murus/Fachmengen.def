DEFINITION MODULE Fachmengen;

(* (c) Christian Maurer   v. 8. Januar 2006 *)

  FROM SYSTEM IMPORT ADDRESS;
IMPORT Farben;
  FROM Schulfaecher IMPORT Faecher, Formate;

TYPE Objekte;
     Praedikate = PROCEDURE (Faecher): BOOLEAN;

PROCEDURE initialisieren (VAR X: Objekte);
(* Vor.: X ist nicht initialisiert. 
   Eff.: X ist initialisiert und leer und hat das Format klein. *)

PROCEDURE terminieren (VAR X: Objekte);
(* Vor.: X ist initialisiert. 
   Eff.: X ist nicht initialisiert. *)

PROCEDURE leer (X: Objekte): BOOLEAN;
(* Vor.: X ist initialisiert. 
   Eff.: Liefert genau dann TRUE, wenn X keine Fächer enthält. *)

PROCEDURE leeren (X: Objekte);
(* Vor.: X ist initialisiert. 
   Eff.: X ist leer. *)

PROCEDURE kopieren (X, X1: Objekte);
(* Vor.: X und X1 sind initialisiert. 
   Eff.: X ist unverändert, X1 enthält die gleichen Fächer wie X. *)

PROCEDURE gleich (X, X1: Objekte): BOOLEAN;
(* Vor.: X und X1 sind initialisiert. 
   Eff.: Liefert genau dann TRUE, wenn X und X1 
         die gleichen Fächer enthalten. *)

PROCEDURE kleinergleich (X, X1: Objekte): BOOLEAN;
(* Vor.: X und X1 sind initialisiert. 
   Eff.: Liefert genau dann TRUE, wenn jedes Fach aus X 
         auch in X1 enthalten ist. *)

PROCEDURE definieren (X: Objekte; F: Faecher);
(* Vor.: X ist initialisiert. 
   Eff.: X besteht nur aus F. *)

PROCEDURE disjunkt (X, X1: Objekte): BOOLEAN;
(* Vor.: X und X1 sind initialisiert.
   Eff.: Liefert genau dann TRUE, wenn kein Fach sowohl in X 
         als auch in X1 vorkommt. *)

PROCEDURE existiert (X: Objekte; VAR F: Faecher; P: Praedikate): BOOLEAN;
(* Vor.: X ist initialisiert. 
   Eff.: Liefert genau dann TRUE, wenn in X ein Fach enthalten ist, 
         auf das P zutrifft. In diesem Fall ist F ein solches Fach; 
         andernfalls ist F leer. *)

PROCEDURE enthalten (X: Objekte; F: Faecher): BOOLEAN;
(* Vor.: X und X1 sind initialisiert. 
   Eff.: Liefert genau dann TRUE, wenn F in X enthalten ist. *)

PROCEDURE einfuegen (X: Objekte; F: Faecher);
(* Vor.: X ist initialisiert. 
   Eff.: X besteht aus X vorher und F. *)

PROCEDURE entfernen (X: Objekte; F: Faecher);
(* Vor.: X ist initialisiert. 
   Eff.: X besteht aus X vorher ohne F. 
         War F nicht in X enthalten, ist nichts verändert. *)

PROCEDURE einschraenken (P, L, X: Objekte);
(* Vor.: P, L und X sind initialisiert.
   Eff.: Wenn L und X kein Fach gemeinsam haben, sind aus P 
         alle Fächer entfernt, die nicht in X enthalten sind. *)

PROCEDURE einschraenken1 (P, L, A1, A2, X: Objekte);
(* Vor.: P, L, A1, A2 und X sind initialisiert. 
         X enthält Fächer aus A1 oder aus A2. 
   Eff.: Wenn L ein Fach aus A1 oder A2 enthält, ist P unverändert. 
         Andernfalls sind aus P sind alle Fächer entfernt, 
         die nicht in A1 oder A2 enthalten sind, und wenn L 
         darüberhinaus kein Fach aus X enthält, sind aus P 
         auch noch die Fächer aus A2 entfernt, die nicht in X 
         enthalten sind, falls X kein Fach mit A1 gemeinsam hat, 
         bzw. auch noch die Fächer aus A1, die nicht in X 
         enthalten sind, falls X kein Fach mit A2 gemeinsam hat. *)

PROCEDURE erstesFach (X: Objekte): Faecher;
(* Vor.: X ist initialisiert. 
   Eff.: Liefert das leere Fach, wenn X kein Fach enthält, sonst das 
         (im Sinne der Ordnung auf den Fächern) erste Fach aus X. *)

PROCEDURE Anzahl (X: Objekte): CARDINAL;
(* Vor.: X ist initialisiert. 
   Eff.: Liefert die Anzahl der Fächer in X. *)

PROCEDURE formatieren (X: Objekte; F: Formate);
(* Vor.: X ist initialisiert. 
   Eff.: X hat das Format F. *)

PROCEDURE faerben (X: Objekte; S, H: Farben.Objekte);
(* Vor.: X ist initialisiert. 
   Eff.: X hat die Farben S und H. *)

PROCEDURE ausgeben (X: Objekte; Z, S: CARDINAL);
(* Vor.: X ist initialisiert. 
   Eff.: X ist ab der Position (Z, S) in seinem Format 
         und seinen Farben auf dem Bildschirm ausgegeben. *)

PROCEDURE vereinigen (X, X1: Objekte);
(* Vor.: X und X1 sind initialisiert. 
   Eff.: X enthält alle Fächer aus X und X1 vorher; 
         X1 ist unverändert. *)

PROCEDURE schneiden (X, X1: Objekte);
(* Vor.: X und X1 sind initialisiert. 
   Eff.: X enthält alle Fächer aus X vorher, die auch in X1 liegen. 
         X1 ist unverändert. *)

PROCEDURE separieren (X, X1: Objekte; P: Praedikate);
(* Eff.: X1 besteht aus genau den Fächern aus X, auf die P zutrifft. 
         Diese Fächer sind aus X entfernt. *)

PROCEDURE komplementieren (X, X1: Objekte);
(* Vor.: X und X1 sind initialisiert. 
   Eff.: Aus X sind genau die Fächer entfernt, die in X1 enthalten sind. 
         X1 ist unverändert. *)

VAR
  alleFaecher, 
  alleMusischenFaecher, alleFremdsprachen, alteSprachen,
  alleNaturwissenschaften,
  Aufgabenfeld1, Aufgabenfeld2, Aufgabenfeld3: Objekte;

END Fachmengen.
